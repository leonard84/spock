apply from: script("publishMaven")

ext.displayName = "Spock Framework - Spring Module"

description = "Spock's Spring Module makes it possible to use Spring's TestContext framework together with Spock. \
Supports Spring 2.5.x, 3.x, and 4.x."

def springVersion = "4.3.5.RELEASE"

if (springVersion.startsWith("2.")) {
  sourceSets.test.groovy.exclude "**/*ContextHierarchyExample.groovy"
}

sourceSets {
  byteBuddyTest {
    compileClasspath += sourceSets.main.output
    compileClasspath += sourceSets.test.output
    runtimeClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.test.output
  }
  cglibTest {
    compileClasspath += sourceSets.main.output
    compileClasspath += sourceSets.test.output
    runtimeClasspath += sourceSets.main.output
    runtimeClasspath += sourceSets.test.output
  }
}

configurations {
  byteBuddyTestImplementation.extendsFrom testImplementation
  byteBuddyTestRuntimeOnly.extendsFrom testRuntimeOnly
  cglibTestImplementation.extendsFrom testImplementation
  cglibTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
  api project(":spock-core")
  implementation "org.springframework:spring-test:$springVersion", provided
  implementation "org.springframework:spring-beans:$springVersion", provided
  implementation "org.springframework:spring-context:$springVersion", provided

  // not used directly at implementation-time, but needed by groovyc
  testImplementation "org.springframework:spring-core:$springVersion"
  testImplementation "org.springframework:spring-jdbc:$springVersion"
  testImplementation "org.springframework:spring-tx:$springVersion"
  testImplementation "javax.inject:javax.inject:1"
  testImplementation libs.groovySql // for groovy.sql.Sql

  testRuntimeOnly libs.h2database
  testRuntimeOnly libs.log4j
  byteBuddyTestRuntimeOnly (project(":spock-core")) {
    capabilities {
      requireCapability "org.spockframework:spock-core-byte-buddy"
    }
  }
  cglibTestRuntimeOnly (project(":spock-core")) {
    capabilities {
      requireCapability "org.spockframework:spock-core-cglib"
    }
  }
}

tasks.test {
  classpath = sourceSets.byteBuddyTest.runtimeClasspath
}

task testCglib(type: Test) {
  systemProperty("cglibOnClasspath", "true")
  classpath = sourceSets.cglibTest.runtimeClasspath
  mustRunAfter test
}

check.dependsOn testCglib

jar {
  manifest {
    attributes(
      'Build-Revision': versioning.info.commit,
      'Specification-Title': project.name,
      'Specification-Version': baseVersion,
      'Specification-Vendor': 'spockframework.org',
      'Implementation-Title': project.name,
      'Implementation-Version': variantLessVersion,
      'Implementation-Vendor': 'spockframework.org',
      'Automatic-Module-Name': 'org.spockframework.spring'
    )
  }
}
