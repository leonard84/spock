apply from: script("publishMaven")

ext.displayName = "Spock Framework - Guice Module"

description = "Spock's Guice Module provides support for testing Guice 2/3 based applications."

configurations {
  guice5RuntimeOnly {
    canBeConsumed = false
    extendsFrom testRuntimeClasspath
  }
}

dependencies {
  api project(":spock-core")
  implementation "com.google.inject:guice:4.2.2", provided

  guice5RuntimeOnly("com.google.inject:guice") {
    version {
      strictly('5.0.1')
    }
  }
}

jar {
  manifest {
    attributes(
      'Build-Revision': versioning.info.commit,
      'Specification-Title': project.name,
      'Specification-Version': baseVersion,
      'Specification-Vendor': 'spockframework.org',
      'Implementation-Title': project.name,
      'Implementation-Version': variantLessVersion,
      'Implementation-Vendor': 'spockframework.org',
      'Automatic-Module-Name': 'org.spockframework.guice'
    )
  }
}

task testGuice5(type: Test) {
  mustRunAfter test
  classpath = configurations.guice5RuntimeOnly + sourceSets.test.output + sourceSets.main.output

}

check.dependsOn testGuice5
